plugins {
    id "base"
    id "wtmp.deps-conventions"
    id("com.pswidersk.python-plugin") version "3.1.10"
    id "wtmp.versioning-conventions"
    id "wtmp.publishing-conventions"
}

import com.pswidersk.gradle.python.VenvTask;

allprojects {
    group = 'usbr.wat.plugins'
    version = versionConventions.getConventionVersion()
}

pythonPlugin {
    pythonVersion = "3.11.0"
    condaVersion = "25.3.1-0"
    condaInstaller = "Miniforge3"
    installDir = file(layout.buildDirectory.dir("python"))
}

def pythonSourceSet = objects.sourceDirectorySet("pythonSource", "pythonSource")
pythonSourceSet.srcDir('src/main/python')

tasks.register("installPackages", VenvTask) {
    dependsOn("envSetup")

    inputs.file("environment.yml")
    outputs.upToDateWhen { !tasks.named("envSetup").get().didWork }

    group = "build"

    venvExec = "conda"
    def envFile = layout.projectDirectory.file("environment.yml").asFile.getAbsolutePath()
    args = ["env", "update", "--name", pythonPlugin.pythonEnvName$python_gradle_plugin.get(), "--file", envFile]

    outputs.files([])
}

tasks.register("buildInstaller", VenvTask) {
    dependsOn("installPackages")

    def pyinstallerDir = layout.buildDirectory.dir("pyinstaller")
    doFirst {
        pyinstallerDir.get().getAsFile().mkdirs()
    }

    group = "build"

    outputs.upToDateWhen { !tasks.named("installPackages").get().didWork }

    inputs.files(pythonSourceSet.getSourceDirectories().files)

    venvExec = "pyinstaller"
    args = ["-y", "${pythonSourceSet.sourceDirectories.asPath}/WAT_Report_Generator.py"]
    workingDir = pyinstallerDir

    outputs.file(pyinstallerDir.get().dir("dist").dir("WAT_Report_Generator").file("WAT_Report_Generator.exe"))
}

tasks.register("zipPython", Zip) {
    dependsOn("buildInstaller")

    group = "build"

    from tasks.named("buildInstaller")
    include '**/**.exe'
    archiveBaseName = 'usbr-actionpanel-python'
    includeEmptyDirs = false
    destinationDirectory = file("$projectDir/build")

    eachFile { fileCopyDetails ->
        fileCopyDetails.setRelativePath(fileCopyDetails.getRelativePath().prepend("HEC-WAT/AutomatedReport"))
    }
}

assemble.dependsOn(tasks.named("zipPython"))

artifacts {
    'default' tasks.named("zipPython")
}

publishing {
    publications {
            maven(MavenPublication) {
                artifactId = "usbr-actionpanel-python"
                artifact source: tasks.named("zipPython"), extension: 'zip'
            }
        }

    // publish to github packages
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/DOI-BOR/WTMP-Python-Plotting")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}