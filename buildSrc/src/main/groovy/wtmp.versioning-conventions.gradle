plugins {
    id "com.palantir.git-version"
    id "org.ajoberstar.grgit"
}

tasks.register("showGit") {
    doFirst {
        def gitInfo = versionDetails()
        println( "Used:    " + getVersion() ) // The select version (the project.version field)
        println( "Tag:     " + gitInfo.lastTag) // actual tag or commit hash(short) if no tag
        println( "Hash:    " + gitInfo.gitHash) // short commit hash
        println( "Branch:  " + gitInfo.branchName) // branch name (null if tag checkout)
        println( "TagBuild:" + gitInfo.isCleanTag) // true if repoi is in dettached head mode(e.g. git checkout <tag>)
    }
}

def versionLabel(gitInfo) {
    def branch = gitInfo.branchName // all branches are snapshots, only tags get released
    if (branch == null && !"$System.env.CI_COMMIT_BRANCH".isEmpty()) {
        branch = "$System.env.CI_COMMIT_BRANCH" // Gitlab CI checks out a commit, not a branch. Grab the branch name from the env vars it sets
    }
    def tag = gitInfo.lastTag
    // tag is returned as is. Branch may need cleanup
    return (branch == null || branch == "null" || branch.isEmpty()) ? tag : branch.replace("/","-") + "-SNAPSHOT"
}

/**
 * When running on the Build System (eg TeamCity) will get BUILD_NUMBER
 * Otherwise, gets the current commit (and status)
 */
String getBuildID() {
    if(project.version.contains("-SNAPSHOT")) {
        def buildNum = System.env.BUILD_NUMBER
        if (!buildNum?.trim()) {
            buildNum = "-${grgit.head().abbreviatedId}"
            def clean = grgit.status().isClean()
            if (!clean) {
                buildNum += "+"
            }
        } else {
            buildNum = "." + buildNum
        }
        return buildNum
    } else {
        // If dirty, return "+"
        return grgit.status().isClean() ? "" : "+"
    }
}

String getShortVersionName() {
    String shortVersion = project.version
    if(shortVersion.contains("-SNAPSHOT")) {
        shortVersion = shortVersion.substring(0, shortVersion.indexOf("-SNAPSHOT"))
    }
    return shortVersion
}

String getConventionVersion() {
    return versionLabel(versionDetails())
}

project.getExtensions().add("versionConventions", this)
